buildscript {
    ext {
        groovyVersion = "3.0.9"
        lombokVersion = "1.18.22"
        jaksonVersion = "2.13.1"
        spockVersion = "2.0-groovy-3.0"
        powermockVersion = "2.0.9"
    }
}

plugins {
    id 'org.springframework.boot' version '2.6.2'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'groovy'
    id 'application'
    id 'checkstyle'
    id 'jacoco'
}

group = 'pl.futurecollars.invoicing'
version = '0.0.1'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {

    implementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    testImplementation "org.codehaus.groovy:groovy:${groovyVersion}"

    testImplementation "org.spockframework:spock-core:${spockVersion}"
    testImplementation "org.spockframework:spock-spring:${spockVersion}"
    testImplementation 'junit:junit:4.13.2'
//    testImplementation "org.mockito:mockito-core:3.+"

//    testImplementation 'org.mockito:mockito-all:2.23.4'

//    testImplementation "org.powermock:powermock-module-junit4:${powermockVersion}"
//    testImplementation "org.powermock:powermock-module-junit4-rule:${powermockVersion}"
//    testImplementation "org.powermock:powermock-api-mockito2:${powermockVersion}"
//    testImplementation "org.powermock:powermock-classloading-xstream:${powermockVersion}"

    implementation 'com.google.guava:guava:30.1.1-jre'

    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    implementation "com.fasterxml.jackson.core:jackson-databind:${jaksonVersion}"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jaksonVersion}"
}

application {
    mainClass = 'pl.futurecollars.invoicing.App'
}

tasks.named('test') {
    useJUnitPlatform()
}

checkstyle {
    showViolations = true
    ignoreFailures = false
    maxWarnings = 0
    maxErrors = 0
}

test {
    finalizedBy jacocoTestReport
    finalizedBy jacocoTestCoverageVerification
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = false
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'BRANCH'
                minimum = 0.85
            }
        }
        rule {
            limit {
                counter = 'LINE'
                minimum = 0.85
            }
        }
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: 'pl/futurecollars/invoicing/App.class')
        }))
    }
}
